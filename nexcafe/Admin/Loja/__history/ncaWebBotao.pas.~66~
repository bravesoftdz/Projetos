unit ncaWebBotao;

interface

uses 
  classes, sysutils, dxBar, Windows, idHttp, cxStyles, dxGDIPlusClasses, Graphics, cxPC, ncaFrmWebTab;

type
  TncaWebBotoes = class;
  
  TncaDownloadBotaoThread = class ( TThread )
  private
     FDownOk : Boolean;
     FImg : String;
  protected
     procedure Execute; override;
  public
    constructor Create(aImg: String);
  end;

  TncaWebBotaoData = object
    Nome      : String;
    Url       : String;
    Blank     : Boolean;
    ShowUrl   : Boolean;
    Sel       : Boolean;
    Toolbar   : Boolean;
    Bold      : Boolean;
    paramSessao,
    paramAdmin,
    paramConta : Boolean;
    
    Posicao   : Byte;
    Gray      : Boolean;
    Width     : Integer;
    Color     : Integer;
    Img       : String;

    procedure Clear;

    function BotaoExiste: Boolean;

    function FromString(S: String): Boolean;
    procedure AssignFrom(B: TncaWebBotaoData);
    function Igual(B: TncaWebBotaoData): Boolean;

    function GetStyle: TcxStyle;
  end;

  TncaWebBotao = class
  private
    FWebBotoes : TncaWebBotoes;
    FData      : TncaWebBotaoData;
    FDownBtn   : TncaDownloadBotaoThread;
    FBtn       : TdxBarLargeButton;
    FStyle     : TcxStyle;
    FTab       : TcxTabSheet;
    FWebTab    : TFrmWebTab;

    procedure OnLoadBtn(Sender: TObject);
    procedure LoadImgBtn;
  public
    constructor Create(aWebBotoes: TncaWebBotoes; aData: TncaWebBotaoData); 

    destructor Destroy; override;

    property Data: TncaWebBotaoData read FData;
    property Tab: TcxTabSheet read FTab;
    property WebTab: TFrmWebTab read FWebTab;
  end;

  TncaWebBotoes = class
  private
    FBotoes : TList;
    FBar : TdxBar;
    FOnClick : TNotifyEvent;
    FStyle : TcxStyle;
    FWidth : Integer;
    FPC   : TcxPageControl;

    function FindBotao(aNome: String): TncaWebBotao;
    procedure UpdateBotao(aData: TncaWebBotaoData);
  public
    constructor Create(aBar: TdxBar; aPC: TcxPageControl; aStyle: TcxStyle; aWidth: Integer);

    procedure FromString(S: String);

    destructor Destroy; override;

    property OnClick: TNotifyEvent
      read FOnClick write FOnClick;
  end;

var
  gWebBotoes : TncaWebBotoes = nil;  

implementation

uses ncClassesBase;

{ TncaWebBotoes }

function UrlBotao(aImg: String): String;
begin
  Result := 'http://botao.nextar.com/'+aImg+'.png';
end;

function PathBotao: String;
begin
  Result := ExtractFilePath(ParamStr(0))+'imgs\';
end;

function ArqBotao(aImg: String): String;
begin
  Result := PathBotao+aImg+'.png';
end;

constructor TncaWebBotoes.Create(aBar: TdxBar; aPC: TcxPageControl; aStyle: TcxStyle; aWidth: Integer);
begin
  FBar := aBar;
  FPC  := aPC;
  FStyle := aStyle;
  FWidth := aWidth;
  FOnClick := nil;
  FBotoes := TList.Create;
end;

destructor TncaWebBotoes.Destroy;
begin
  while FBotoes.Count>0 do begin
    TObject(FBotoes[0]).Free;
    FBotoes.Delete(0);
  end;
  FBotoes.Free;
end;

function SplitBotoes(S: String) : TStrings;
var P : Integer;

function GetPos: Boolean;
begin
  P := Pos('[', S);
  Result := (P>0);
end;

begin
  Result := TStringList.Create;
  while GetPos do begin
    Delete(S, 1, P);
    P := Pos(']', S);
    if P>0 then begin
      Result.Add(Copy(S, 1, P-1));
      Delete(S, 1, P);
    end else 
      S := '';
  end;
end;

function TncaWebBotoes.FindBotao(aNome: String): TncaWebBotao;
var i: integer;
begin
  for i := 0 to FBotoes.Count - 1 do
    if TncaWebBotao(FBotoes[i]).FData.Nome=aNome then begin
      Result := TncaWebBotao(FBotoes[i]);
      Exit;
    end;
  Result := nil;
end;

procedure TncaWebBotoes.FromString(S: String);
var 
  sl: TStrings;
  sl2: TStrings;
  B : TncaWebBotaoData;
  i : Integer;
begin
  sl2 := TStringList.Create;
  sl := SplitBotoes(S);
  try
    for i := 0 to sl.Count-1 do 
      if B.FromString(sl[i]) then begin
        sl2.Add(B.Nome);
        UpdateBotao(B);
      end;

    for i := FBotoes.Count -1 downto 0 do 
      if sl2.IndexOf(TncaWebBotao(FBotoes[i]).FData.Nome)=-1 then begin
        TObject(FBotoes[i]).Free;
        FBotoes.Delete(i);
      end;
  finally
    sl2.Free;
    sl.Free;
  end;
end;

procedure TncaWebBotoes.UpdateBotao(aData: TncaWebBotaoData);
var B : TncaWebBotao;
begin
  B := FindBotao(aData.Nome);
  if (B<>nil) and (not B.FData.Igual(aData)) then begin
    FBotoes.Remove(B);
    B.Free;
    B := nil;
  end;

  if B=nil then begin
    B := TncaWebBotao.Create(Self, aData);
    FBotoes.Add(B);
  end;
end;

{ TncaWebBotaoData }

procedure TncaWebBotaoData.AssignFrom(B: TncaWebBotaoData);
begin
  Nome      := B.Nome;
  Url       := B.Url;
  Blank     := B.Blank;
  ShowUrl   := B.ShowUrl;
  Sel       := B.Sel;
  Toolbar   := B.Toolbar;
  Posicao   := B.Posicao;
  Img       := B.Img;
  Bold      := B.Bold;
  Gray      := B.Gray;
  Width     := B.Width;
  Color     := B.Color;
  paramSessao := B.paramSessao;
  paramConta := B.paramConta;
  paramAdmin := B.paramAdmin;
end;

function TncaWebBotaoData.BotaoExiste: Boolean;
begin
  Result := FileExists(ArqBotao(Img));
end;

procedure TncaWebBotaoData.Clear;
begin
  Nome      := '';
  Url       := '';
  Blank     := False;
  ShowUrl   := False;
  Sel       := False;
  Toolbar   := False;
  Posicao   := 0;
  Img       := '';
  Bold      := False;
  Gray      := False;
  Width     := 0;
  Color     := 0;

  paramSessao := False;
  paramAdmin := False;
  paramConta := False;
end;

function TncaWebBotaoData.FromString(S: String): Boolean;
var sl: TStrings;
begin
  Clear;
  sl := SplitParams(S);
  try
    Nome := RemoveAspas(sl.Values['nome']);
    Posicao := StrToIntDef(sl.Values['pos'], 0);
    Img := RemoveAspas(sl.Values['img']);
    Url := RemoveAspas(sl.Values['url']);
    Blank := (sl.Values['blank']='1');
    Gray  := (sl.Values['gray']<>'0');
    Width := StrToIntDef(sl.Values['width'], 0);
    Color := StrToIntDef(sl.Values['color'], 0);
    Bold  := (sl.Values['bold']='1');
    Toolbar := False; //(sl.Values['toolbar']='1');
    ShowUrl := False; //(sl.Values['showurl']='1');
    Sel := (sl.Values['sel']='1');

    paramSessao := (sl.Values['paramSessao']='1');
    paramAdmin  := (sl.Values['paramAdmin']='1');
    paramConta  := (sl.Values['paramConta']='1');

    Result := (Nome>'') and (Posicao in [0..1]) and (Img > '') and (Url>'');
  finally
    sl.Free;
  end;
end;

function TncaWebBotaoData.GetStyle: TcxStyle;
begin
  if Bold or (Color<>0) then begin
    Result := TcxStyle.Create(nil);
    Result.TextColor := Color;
    if Bold then
      Result.Font.Style := [fsBold];
  end else
    Result := nil;
end;

function TncaWebBotaoData.Igual(B: TncaWebBotaoData): Boolean;
begin
  Result := False;
  if Nome<>B.Nome then Exit;
  if Url<>B.Url then Exit;
  if Blank<>B.Blank then Exit;
  if ShowUrl<>B.ShowUrl then Exit;
  if Sel<>B.Sel then Exit;
  if Toolbar<>B.Toolbar then Exit;
  if Posicao<>B.Posicao then Exit;
  if Img<>B.Img then Exit;

  if Bold<>B.Bold then Exit;
  if Gray<>B.Gray then Exit;
  if Color<>B.Color then Exit;
  if Width<>B.Width then Exit;

  if paramSessao<>B.paramSessao then Exit;
  if paramAdmin<>B.paramAdmin then Exit;
  if paramConta<>B.paramConta then Exit;
  
  Result := True;
end;

{ TncaWebBotao }

constructor TncaWebBotao.Create(aWebBotoes: TncaWebBotoes; aData: TncaWebBotaoData);
begin
  FWebBotoes := aWebBotoes;
  FDownBtn := nil;
  FData.AssignFrom(aData);
  FStyle := FData.GetStyle;
  FBtn := TdxBarLargeButton.Create(FWebBotoes.FBar.Owner);
  FBtn.BarManager := FWebBotoes.FBar.BarManager;
  FBtn.Caption := aData.Nome;
  FBtn.OnClick := FWebBotoes.OnClick;
  FBtn.Visible := ivNever;
  FBtn.Tag := Integer(Self);
  if Assigned(FStyle) then
    FBtn.Style := FStyle else
    FBtn.Style := FWebBotoes.FStyle;
  FBtn.AutoGrayScale := FData.Gray;

  if FData.Width<FWebBotoes.FWidth then
    FBtn.Width := FWebBotoes.FWidth else
    FBtn.Width := FData.Width;
  if FData.BotaoExiste then 
    LoadImgBtn
  else begin
    FDownBtn := TncaDownloadBotaoThread.Create(FData.Img);
    FDownBtn.OnTerminate := OnLoadBtn;
    FDownBtn.Resume;
  end;
  with FWebBotoes.FBar.ItemLinks.Add do begin
    Item := FBtn;
    if FData.Posicao=0 then 
      Index := 0;
  end;

  if FData.Blank then begin
    FTab := nil;
    FWebTab := nil;
  end
  else begin
    FTab := TcxTabSheet.Create(nil);
    FTab.PageControl := FWebBotoes.FPC;

    FWebTab := TFrmWebTab.Create(nil);
    FWebTab.paramSessao := FData.paramSessao;
    FWebTab.paramAdmin := FData.paramAdmin;
    FWebTab.paramConta := FData.paramConta;
    FWebTab.Paginas.Parent := Ftab;
    FWebTab.URL := FData.Url;
    FWebTab.showToolbar := FData.Toolbar;
    if FData.ShowUrl then
      FWebTab.showUrl := suBase else
      FWebTab.showUrl := suNao;
  end;

  if not FData.Blank then begin
    FBtn.AllowAllUp := False;
    FBtn.ButtonStyle := bsChecked;
    FBtn.GroupIndex := 1;
  end;
end;

destructor TncaWebBotao.Destroy;
begin
  if Assigned(FDownBtn) then begin
    FDownBtn.OnTerminate := nil;
    FDownBtn.Terminate;
  end;
  FBtn.Free;
  if Assigned(FStyle) then FStyle.Free;
  if Assigned(FWebTab) then FWebTab.Free;
  
  if Assigned(FTab) then FTab.Free;
  
  inherited;
end;

procedure TncaWebBotao.LoadImgBtn;
var P: TdxPngImage;
begin
  try
    P := TdxPngImage.Create;
    try
      P.LoadFromFile(ArqBotao(FData.Img));
      FBtn.LargeGlyph := P.GetAsBitmap;
      FBtn.Visible := ivAlways;
    finally
      P.Free;
    end;
  except
  end;
end;

procedure TncaWebBotao.OnLoadBtn(Sender: TObject);
begin
  if TncaDownloadBotaoThread(Sender).FDownOk then
    LoadImgBtn;
end;

{ TncaDownloadBotaoThread }

constructor TncaDownloadBotaoThread.Create(aImg: String);
begin
  FImg := aImg;
  FDownOk := False;
  inherited Create(True);
  FreeOnTerminate := True;
end;

function DownloadArq(aFrom, aTo: String): Boolean;
var S: TFileStream;
begin
  S := TFileStream.Create(aTo, fmCreate);
  try
    with TidHttp.Create(nil) do
    try
      try
        Get(aFrom, S);
        Result := True;
      except
      end;
    finally
      Free;
    end;
  finally
    S.Free;
  end;
end;

procedure TncaDownloadBotaoThread.Execute;
var 
  TryMS,
  NextTry: Cardinal;
  aFrom, aTo: String;
begin
  ForceDirectories(PathBotao);
  NextTry := GetTickCount;
  TryMS := 0;
  aFrom := UrlBotao(FImg);
  aTo := ArqBotao(FImg);
  FDownOk := False;
  repeat
    if GetTickCount>=NextTry then begin
      FDownOk := DownloadArq(aFrom, aTo);
      if not FDownOk then begin
        if TryMS<(60*1000*30) then
          TryMS := TryMS+5000;
        NextTry := GetTickCount+TryMS;
      end;
    end else
      Sleep(100);
  until (Terminated) or FDownOk;
end;

end.
