unit nexUrls;

interface

uses Sysutils, Classes, idHttp, md5, SyncObjs, ExtCtrls, Windows;

type

  TnexUrlsRefresh = class ( TThread )
  protected
    procedure Execute; override;

    procedure Get;
  public
    ResOk : Boolean;
    ResString : String;
    constructor Create;
  end;
  
  TnexUrls = class ( TStringList )
  private
    FDef : TStrings;
    FForce : TStrings;
    FCS : TCriticalSection;
    FTimer : TTimer;
    FOnRefresh: TNotifyEvent;
    FRefreshTh : TnexUrlsRefresh;
    FKeepRefreshing : Boolean;
    function GetOnRefresh: TNotifyEvent;
    procedure SetOnRefresh(const Value: TNotifyEvent);

    function _Values(aName: String): String;
    function GetTextStr: String;
    procedure SetTextStr(const Value: String);

    procedure OnTimer(Sender: TObject);
    procedure OnRefreshTerminate(Sender: TObject);
  public
    function Url(aNome: String; aParams: String = ''; aAddLang: Boolean = True): String;
    function CustomUrl(aNome, aSufix, aParams: String; aAddLang: Boolean): String;
    procedure Refresh;
    constructor Create;
    destructor Destroy; override;

    procedure SetString(aValue: String);

    procedure KeepRefreshing;

    function SkyWidth(aScreenWidth: Integer): Integer;

    function AppTimeMS: Cardinal;
    function AppRetryMS: Cardinal;

    function TTLms: Cardinal;

    function AsString: String;

    property Text: String
      read GetTextStr write SetTextStr;

    property OnRefresh: TNotifyEvent
      read GetOnRefresh write SetOnRefresh;
  end;

  function AddParamUrl(aUrl, aParams: String): String;

var
  gUrls : TnexUrls;  

implementation

uses
  ncDebug, uNexTransResourceStrings_PT;

  threadvar 
    gUrlUsada : String;

{ TnexUrls }


const
  salt = '>LKHASDIUywefd7kdsf_)(*!@Tkjhasfdkxzxxx7777!38213zxc%nbv';

  maxnexttry = 30 * 60 * 1000;

  incnexttry = 5000;

function TnexUrls.AppRetryMS: Cardinal;
const 
  minuto = 60 * 1000;
  hora = 60 * minuto;
begin
  Result := StrToIntDef(_Values('app_time'), 5) * minuto;
  if (Result < (5*minuto)) or (Result > (24*hora)) then
    Result := 5*minuto;
end;

function TnexUrls.AppTimeMS: Cardinal;
const 
  minuto = 60 * 1000;
  hora = 60 * minuto;
begin
  Result := StrToIntDef(_Values('app_time'), 60) * minuto;
  if (Result < (5*minuto)) or (Result > (24*hora)) then
    Result := hora;
end;

function TnexUrls.AsString: String;
begin
  FCS.Enter;
  try
    Result := Text;
  finally
    FCS.Leave;
  end;
end;

function ForceFName: String;
begin 
  Result := ExtractFilePath(ParamStr(0))+'forceurl.ini';
end;

constructor TnexUrls.Create;
begin
  inherited;
  FKeepRefreshing := False;
  
  FTimer := TTimer.Create(nil);
  FTimer.OnTimer := OnTimer;
  FTimer.Enabled := False;

  FRefreshTh := nil;
  
  FForce := TStringList.Create;
  if FileExists(ForceFName) then
    FForce.LoadFromFile(ForceFName);
  
  FDef := TStringList.Create;
  FDef.Values['contas_app']                := 'http://contas.nextar.com.br/contas/app';
  FDef.Values['contas_app_admin']          := 'http://contas.nextar.com.br/contas/app';
  FDef.Values['contas_app_server']         := 'http://contas.nextar.com.br/contas/app';
  FDef.Values['contas_finalizatrial']      := 'http://contas.nextar.com.br/contas/finalizatrial';
  FDef.Values['contas_infoplanos']         := 'http://contas.nextar.com.br/contas/infoplanos/';
  FDef.Values['info_server']               := 'http://contas2.nextar.com.br/contas/info/server';

  
  FDef.Values['contas_obteminfocampanha']  := 'http://contas.nextar.com.br/contas/obteminfocampanha';
  FDef.Values['contas_obtemchaveseg']      := 'http://contas.nextar.com.br/contas/obtemchaveseg';
  FDef.Values['contas_corrigiremail']      := 'http://contas.nextar.com.br/contas/corrigiremail';
  FDef.Values['contas_criarcontabasica']   := 'http://contas.nextar.com.br/contas/criarcontabasica';
  FDef.Values['contas_ativar']             := 'http://contas.nextar.com.br/contas/ativar';
  FDef.Values['contas_esquecisenha']       := 'http://contas.nextar.com.br/contas/esquecisenha';
  FDef.Values['contas_reenviaconfirmacao'] := 'http://contas.nextar.com.br/contas/reenviaconfirmacao';
  FDef.Values['contas_obtemregistro']      := 'http://contas.nextar.com.br/contas/obtemregistro';
  FDef.Values['contas_transferir']         := 'http://contas.nextar.com.br/contas/transferir';
  FDef.Values['contas_abriuadmin']         := 'http://contas.nextar.com.br/contas/abriuadmin';
  FDef.Values['contas_assinar']            := 'http://contas.nextar.com.br/contas/assinar';
  FDef.Values['contas_confirmartrial']     := 'http://contas.nextar.com.br/contas/confirmartrial';
  FDef.Values['contas_upgradetrial']       := 'http://contas.nextar.com.br/contas/upgradetrial';
  FDef.Values['contas_dataupgradeplano']   := 'http://contas.nextar.com.br/contas/dataupgradeplano';
  FDef.Values['contas_fazupgradeplano']    := 'http://contas.nextar.com.br/contas/fazupgradeplano';

  FDef.Values['pag_suporte_free']          := 'http://docs.nextar.com.br/969cfbfe34910c2c0da218709437b2d4';
  FDef.Values['pag_suporte_pro']           := 'http://docs.nextar.com.br/9fff1a8208287c95a378997d1083aeca';
  FDef.Values['pag_suporte_premium']       := 'http://docs.nextar.com.br/1c8ab77f89947c4c9f44056cd3e9cdfd';

  FDef.Values['contas_setflags']           := 'http://contas.nextar.com.br/contas/setflags';
  FDef.Values['contas_getflags']           := 'http://contas.nextar.com.br/contas/getflags';

  FDef.Values['contas_cep']                := 'http://contas.nextar.com.br/cep';

  FDef.Values['kapi_registro']             := 'http://contas.nextar.com.br/kite/registro';
  FDef.Values['kapi_token']                := 'http://contas.nextar.com.br/kite/token';
  FDef.Values['kapi_consultaip']           := 'http://contas.nextar.com.br/kite/consultaip';
  FDef.Values['kapi_employeepass']         := 'http://contas.nextar.com.br/kite/profile/employeepass';
  FDef.Values['kapi_emailcaixa']           := 'http://contas.nextar.com.br/kite/emailcaixa';
  FDef.Values['track']                     := 'http://contas.nextar.com.br/track';
  FDef.Values['nexmsg']                    := 'http://contas.nextar.com.br/nexmsgs';
  FDef.Values['nextabs']                   := 'http://contas.nextar.com.br/nextabs';
  FDef.Values['autoconninfo']              := 'http://contas.nextar.com.br/autoconninfo';
  FDef.Values['dbapi_setup']               := 'http://contas.nextar.com.br/dbapi/setup';
  FDef.Values['scnt']                      := 'http://contas.nextar.com.br/scnt';
  FDef.Values['mailer']                    := 'http://contas.nextar.com.br/mailer';
  FDef.Values['img_botao']                 := 'http://botao.nextar.com';

  FDef.Values['ad_toolbar']                := 'http://ads.nexcafe.com.br/toolbar';
  FDef.Values['ad_sky']                    := 'http://ads.nexcafe.com.br/sky';
  FDef.Values['ad_home']                   := 'http://ads.nexcafe.com.br/open';

  FDef.Values['ttl']                       := '60';
  FDef.Values['app_time']                  := '60';
  FDef.Values['app_retry']                 := '5';
  FDef.Values['nexapp_cards']              := 'http://api.app.nextar.com/cards';
  FDef.Values['nexapp_products']           := 'http://api.app.nextar.com/products';
  FDef.Values['nexapp_reset']              := 'http://api.app.nextar.com/reset';
  FDef.Values['cloudbk']                   := 'http://cloud.nextar.com/backup';
  
  
  FCS := TCriticalSection.Create;
  FOnRefresh := nil;
end;

function TnexUrls.CustomUrl(aNome, aSufix, aParams: String;
  aAddLang: Boolean): String;
begin
  Result := FForce.Values[aNome];

  if Result='' then begin
    Result := _Values(aNome);
    if Result='' then begin
      Refresh;
      Result := _Values(aNome);
    end;
    if Result='' then
      Result := FDef.Values[aNome];
  end;

  if aSufix>'' then begin
    if Copy(Result, Length(Result), 1)<>'/' then Result := Result + '/';
    Result := Result + aSufix;
  end;
  
  if aAddLang then
  if Pos('lang=', aParams)<1 then begin
    if aParams>'' then aParams := aParams + '&';
    aParams := aParams + 'lang='+SLingua;
  end;

  Result := AddParamUrl(Result, aParams);

  DebugMsg(Self, 'Url: '+Result);

  gUrlUsada := Result;  
end;

destructor TnexUrls.Destroy;
begin
  FTimer.Free;
  FDef.Free;
  FCS.Free;
  FForce.Free;
  inherited;
end;

function TnexUrls.GetOnRefresh: TNotifyEvent;
begin
  FCS.Enter;
  try
    Result := FOnRefresh;
  finally
    FCS.Leave;
  end;
end;

function TnexUrls.GetTextStr: String;
begin
  Result := inherited Text;
end;

procedure TnexUrls.KeepRefreshing;
begin
  FKeepRefreshing := True;
  Refresh;
end;

procedure TnexUrls.OnRefreshTerminate(Sender: TObject);
var 
  ResOk: Boolean;
  ResString : String;
begin
  ResOk := FRefreshTh.ResOk;
  ResString := FRefreshTh.ResString;
  FRefreshTh := nil;
  if ResOk then begin
    DebugMsg(Self, 'OnRefreshTerminate - ResOk');
    SetString(ResString);
    if FKeepRefreshing then begin
      FTimer.Interval := TTLms;
      FTimer.Enabled := True;
    end;  
    if Assigned(FOnRefresh) then FOnRefresh(Self);
  end else 
  begin
    DebugMsg(Self, 'OnRefreshTerminate - Falhou');
    FTimer.Interval := 5000;
    FTimer.Enabled := True;        
  end;
end;

procedure TnexUrls.OnTimer(Sender: TObject);
begin
  FTimer.Enabled := False;
  Refresh;
end;

procedure TnexUrls.Refresh;
begin
  if Assigned(FRefreshTh) then Exit;
  FRefreshTh := TnexUrlsRefresh.Create;
  FRefreshTh.OnTerminate := OnRefreshTerminate;
  FREfreshTh.Resume;
end;
  
procedure TnexUrls.SetOnRefresh(const Value: TNotifyEvent);
begin
  FCS.Enter;
  try
    FOnRefresh := Value;
  finally
    FCS.Leave;
  end;
end;

procedure TnexUrls.SetString(aValue: String);
begin
  FCS.Enter;
  try
    Text := aValue;
  finally
    FCS.Leave;
  end;
end;

procedure TnexUrls.SetTextStr(const Value: String);
begin
  inherited Text := Value;
end;

type
  TSkyWidth = record
    swScreen : Integer;
    swWidth : Integer;
  end;

function StrToSkyWidth(S: String; var SW: TSkyWidth): Boolean;  
var P: Integer;
begin
  Result := False;
  P := Pos('=', S);
  if P<2 then Exit;
  sw.swScreen := StrToIntDef(Copy(S, 1, P-1), 0);
  sw.swWidth := StrToIntDef(Copy(S, P+1, 4), 0);
  Result := (sw.swScreen>0) and (sw.swWidth>0) and (sw.swWidth<sw.swScreen);
end;
  
function TnexUrls.SkyWidth(aScreenWidth: Integer): Integer;
var 
  S : String;
  P  : Integer;
  sw    : TskyWidth;
begin
  Result := 160;
  S := _Values('skysizemap');
  if S='' then Exit;

  repeat
    P := Pos('(', S);
    if P>0 then begin
      System.Delete(S, 1, P);
      P := Pos(')', S);
      if P>0 then begin
        if StrToSkyWidth(Copy(S, 1, P-1), sw) then
        if aScreenWidth<=sw.swScreen then begin
          Result := sw.swWidth;
          Exit;
        end;
        System.Delete(S, 1, P);
      end;
    end;
  until (P<1);
end;

function TnexUrls.TTLms: Cardinal;
const
  minuto = 60 * 1000;
  hora = 60 * minuto;
begin
  Result := StrToIntDef(_Values('ttl'), 60) * minuto;
  if (Result < (5*minuto)) or (Result>(24*hora))then
    Result := hora;
  DebugMsg(Self, 'TTLms: '+IntToStr(Result));  
end;

function AddParamUrl(aUrl, aParams: String): String;
begin
  Result := aUrl;
  if aParams='' then Exit;

  if Pos('?', Result)<1 then begin
    if Copy(Result, Length(Result), 1)<>'/' then
      Result := Result + '/';
    Result := Result + '?';
  end else
    Result := Result + '&';
    
  Result := Result + aParams;
end;

function TnexUrls.Url(aNome: String; aParams: String = ''; aAddLang: Boolean = True): String;
begin
  Result := CustomUrl(aNome, '', aParams, aAddLang);
{  Result := FForce.Values[aNome];

  if Result='' then begin
    Result := _Values(aNome);
    if Result='' then begin
      Refresh;
      Result := _Values(aNome);
    end;
    if Result='' then
      Result := FDef.Values[aNome];
  end;

  if aAddLang then
  if Pos('lang=', aParams)<1 then begin
    if aParams>'' then aParams := aParams + '&';
    aParams := aParams + 'lang='+SLingua;
  end;

  Result := AddParamUrl(Result, aParams);

  DebugMsg(Self, 'Url: '+Result);

  gUrlUsada := Result; } 
end;

function TnexUrls._Values(aName: String): String;
begin
  FCS.Enter;
  try
    Result := Values[aName];
  finally
    FCS.Leave;
  end;
end;

{ TnexUrlsRefresh }

constructor TnexUrlsRefresh.Create;
begin
  inherited Create(True);
  ResOk := False;
  ResString := '';
  FreeOnTerminate := True;
end;

procedure TnexUrlsRefresh.Execute;
var NextTry : Cardinal;
begin
  NextTry := 0;
  ResOk := False;
  repeat
    try
      if GetTickCount>=NextTry then begin
        Get;
        NextTry := GetTickCount + 10000;
      end;
      Sleep(10);
    except
      On E: Exception do DebugEx(Self, 'Execute', E);
    end;
  until ResOk or Terminated;
end;

procedure TnexUrlsRefresh.Get;
var h: TidHttp; sl: TStrings; S: String;
begin
  H := TidHttp.Create(nil);
  try
    ResOk := False;
    H.HandleRedirects := True;
    sl := TStringList.Create;
    try
      try
        sl.Text := H.Get('http://setup.contas.nextar.com.br/?random='+IntToStr(Random(999999)));
        DebugMsg('TnexUrlsRefresh.Get - ' + sl.Text);
      except
        on E: Exception do begin
          DebugMsg('TnexUrlsRefresh.Get - Exception:' + sl.Text);
          Exit;
        end;
      end;
      S := sl.Values['chave'];
      if S='' then Exit;
      sl.Delete(sl.IndexOfName('chave'));
      if not sameText(S, getmd5str(sl.Text+salt)) then 
      begin
        DebugMsg('TnexUrlsRefresh.Get - Chave Invalida: '+S+' - Chave servidor: '+getmd5str(sl.Text+salt));
        Exit;
      end;
      ResOk := True;
      ResString := sl.Text;  
    finally
      sl.Free;
    end;
  finally
    H.Free;
  end;
end;

initialization
  gUrls := TnexUrls.Create;

finalization
  gUrls.Free;


end.
